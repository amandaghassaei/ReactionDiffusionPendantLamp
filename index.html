<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>Reaction-Diffusion Shader</title>

    <link rel="stylesheet" type="text/css" href="main.css">

    <script type="text/javascript" src="GlBoilerplate.js"></script>
    <script type="text/javascript" src="main.js"></script>
    <script id="2d-vertex-shader" type="x-shader/x-vertex">

        attribute vec2 a_position;
        uniform float u_flipY;

        void main() {
           gl_Position = vec4(a_position.x, u_flipY*a_position.y, 0, 1);
        }
    </script>

    <script id="2d-render-shader" type="x-shader/x-fragment">
        precision mediump float;

        uniform sampler2D u_image;
        uniform vec2 u_textureSize;

        const float COLOR_MIN = 0.2, COLOR_MAX = 0.4;

        void main() {
            float v = (COLOR_MAX - texture2D(u_image, gl_FragCoord.xy / u_textureSize).y) / (COLOR_MAX - COLOR_MIN);
            gl_FragColor = vec4(v, v, v, 1);
        }
        </script>

    <script id="2d-fragment-shader" type="x-shader/x-fragment">
        precision mediump float;

        //texture array
        uniform sampler2D u_image;//u, v, --, -- = r, g, b, a

        uniform vec2 u_textureSize;

        uniform vec2 u_mouseCoord;
        uniform float u_mouseEnable;

        const float fParam = 0.0545;
        const float kParam = 0.062;
	    const float DuParam = 0.2;
	    const float DvParam = 0.1;
	    const float dt = 1.0;

        uniform float u_renderFlag;

        float noise(float x) {
          return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        void main() {

            vec2 fragCoord = gl_FragCoord.xy;

            vec2 currentState = texture2D(u_image, fragCoord/u_textureSize).xy;
            float u = currentState.x;
            float v = currentState.y;

            if (u_renderFlag == 1.0){
                float vRender = (0.4 - v) / (0.2);
                gl_FragColor = vec4(vRender, vRender, vRender, 1);
                return;
            }

            vec2 n = fragCoord + vec2(0.0, 1.0);//north
            vec2 s = fragCoord + vec2(0.0, -1.0);//south
            vec2 e = fragCoord + vec2(1.0, 0.0);//east
            vec2 w = fragCoord + vec2(-1.0, 0.0);//west


            if (u_mouseEnable == 1.0){
                vec2 pxDistFromMouse = (fragCoord - u_mouseCoord)*(fragCoord - u_mouseCoord);
                float tol = 10.0;
                if (pxDistFromMouse.x < tol && pxDistFromMouse.y < tol){
                    u = 0.5 + 0.1 * 0.02 - 0.01;
                    v = 0.25 + 0.1 * 0.02 - 0.01;
                }
            }

            vec2 grad = texture2D(u_image, n/u_textureSize).xy + texture2D(u_image, s/u_textureSize).xy
                + texture2D(u_image, e/u_textureSize).xy + texture2D(u_image, w/u_textureSize).xy - 4.0*currentState;

            float uvSq = u*v*v;
            vec2 delta = dt*vec2(DuParam*grad.x - uvSq + fParam*(1.0 - u),
            DvParam*grad.y + uvSq - (fParam + kParam)*v);

            gl_FragColor = vec4(u + delta.x, v + delta.y, 0, 1);
        }
    </script>
</head>
<body>

<canvas id="glcanvas"></canvas>

</body>
</html>